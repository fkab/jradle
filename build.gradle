apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

repositories {
   mavenCentral()
   jcenter()
}


dependencies {
   testCompile group: 'junit', name: 'junit', version: '4.8.+'
   testCompile "org.mockito:mockito-core:1.10.0"
   compile fileTree (dir: 'lib', includes: ['*.jar'])
}

/*
 * integration tests
 */
sourceSets {
  integrationTest {
    compileClasspath = sourceSets.main.output + configurations.testRuntime
    runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    java {
      srcDir 'src/integrationTest/java'
    }
  }
}

task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
}

/*
 * Project cleaner. Run before "git commit"
 */
task cleanAndClear(dependsOn: [
    cleanEclipse,
    cleanIdea,
    cleanIdeaWorkspace,
    clean
]) {
    delete 'bin', 'out', '.idea', '.settings'
}
